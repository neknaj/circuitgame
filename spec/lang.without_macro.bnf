<file> := <components>

<components> := <component> <components> | ε
<component> := <using> | <import> | <module> | <graphical> | <test>

<mName> := /([0-9a-zA-Z]|_)+/
<mId> := /([0-9a-zA-Z]|_)+/
<int> := /[0-9]+/
<fileName> := "\"" ( not '\"' )+ "\"" | ( not ( <Space> | not ) ";" )+

<mType> := <int> <rArrow> <int>
<mTypeConcrete> := <int> <rArrow> <int>
<mIds> := <mId> <mIds> | ε
<mIO> := "(" <mIds> ")"

<rArrow> := ">" | "->" | "=>" | "~>"
<lArrow> := "<" | "<-" | "<=" | "<~"
<import_keyword> := "import" | "Import" | "IMPORT"
<export_keyword> := "export" | "Export" | "EXPORT"
<using_keyword> := "using" | "Using" | "USING" | "use" | "Use" | "USE"
<module_keyword> := "module" | "Module" | "MODULE" | "def" | "Def" | "DEF"
<func_module_keyword> := "func" | "Func" | "FUNC" | "fn" | "Fn" | "FN"
<graphical_keyword> := "graphical" | "Graphical" | "GRAPHICAL"
<test_keyword> := "test" | "Test" | "TEST"
<true>  := "t" | "T" | "h" | "H" | "1"
<false> := "f" | "F" | "l" | "L" | "0"
<gate_separator> := ":" | "=" | ":=" | "::="

<using> := <using_keyword> "nor" ":" "2" <rArrow> "1" ";"
<import> := <import_keyword> "{" <importModules> "}" <from_keyword> <fileName> ";"
<module> := <export_keyword>? <module_keyword> <mName> <mIO> <rArrow> <mIO> "{" <gates> "}"
<test> := <test_keyword> <mName> ":" <mType> "{" <testPatterns> "}"

<importModule> := <mId>
<importModules> := <importModule> <importModules> | ε

<gate> := <mIds> <gate_separator> <mName> <lArrow>? <mIds> ";"
<gates> := <gate> <gates> | ε

<bool> := <true> | <false>
<values> := <bool> <values> | ε
<testPattern> := <values> <rArrow> <values> ";"
<testPatterns> := <testPattern> <testPatterns> | ε


<graphical> := <graphical_keyword> <mName> ":" <imgSize> "{" <pixels> "}"
<imgSize> := "auto" | /[0-9]+/ "x"? /[0-9]+/
<coord> := /[0-9]+/ /[0-9]+/
<color> := "#" /[0-9]{6}/
<ioName> := "i" /[0-9]+/ | "o" /[0-9]+/
<pixel> := <coord> <gate_separator> <ioName> <lArrow>? <color> <color> ";"
<pixels> := <pixel> <pixels> | ε


# 以下は、色んな所に良い感じに入れられるが、BNFが読みにくくなるので、上記の文法からは消し去ってある

<LineComment> := ("//" | "#") /[^\r\n]/ ( "\n" | "\r\n" )

# 比較的どこにでも入れられる
<Space> := " " | "\t" | "\n" | "\r"
# <file>直下と、<module>,<using>の{}直前と中で使える, コメントを許す
<Separator> := ( <Space>+ | <LineComment> )*
# <mId>と<bool>の区切りに使える, コンマ区切りを許す
<ValueSeparator> := <Space>+ | <Space>* "," <Space>*