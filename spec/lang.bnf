<file> := <components>

<components> := <component> <components> | ε
<component> := <using> | <module> | <test>

<mName> := /([0-9a-zA-Z]|_)+/
<mId> := /([0-9a-zA-Z]|_)+/
<int> := /[0-9]+/

<mType> := <int> <rArrow> <int>
<mTypeConcrete> := <int> <rArrow> <int>
<mIds> := <mId> <mIds> | ε
<mIO> := "(" <mIds> ")"

<rArrow> := ">" | "->" | "=>" | "~>"
<lArrow> := "<" | "<-" | "<=" | "<~"
<using_keyword> := "using" | "Using" | "USING" | "use" | "Use" | "USE"
<module_keyword> := "module" | "Module" | "MODULE" | "def" | "Def" | "DEF"
<test_keyword> := "test" | "Test" | "TEST"
<true>  := "t" | "T" | "h" | "H" | "1"
<false> := "f" | "F" | "l" | "L" | "0"
<gate_separator> := ":" | "=" | ":=" | "::=" | <lArrow>

<using> := <using_keyword> "nor" ":" "2" <rArrow> "1" ";"
<module> := <module_keyword> <mName> <mIO> <rArrow> <mIO> "{" <gates> "}"
<test> := <test_keyword> <mName> ":" <mType> "{" <testPatterns> "}"

<gate> := <mIds> <gate_separator> <mName> <lArrow>? <mIds> ";"
<gates> := <gate> <gates> | ε

<bool> := <true> | <false>
<values> := <bool> <values> | ε
<testPattern> := <values> <rArrow> <values> ";"
<testPatterns> := <testPattern> <testPatterns> | ε


# 以下は、色んな所に良い感じに入れられるが、BNFが読みにくくなるので、上記の文法からは消し去ってある

<LineComment> := ("//" | "#") /[^\r\n]/ ( "\n" | "\r\n" )

# 比較的どこにでも入れられる
<Space> := " " | "\t" | "\n" | "\r"
# <file>直下と、<module>,<using>の{}直前と中で使える, コメントを許す
<Separator> := ( <Space>+ | <LineComment> )*
# <mId>と<bool>の区切りに使える, コンマ区切りを許す
<ValueSeparator> := <Space>+ | <Space>* "," <Space>*